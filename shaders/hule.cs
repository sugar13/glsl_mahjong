// compute shader
// åˆ¤æ–­å’Œäº†

#version 460 core

layout (local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

layout (binding = 0) readonly buffer in_buffer
{
    uint shoupai[14]; // å‚æ•°ï¼šæ‰‹ç‰Œï¼Œå¯ä»¥æ˜¯14ã€11ã€8ã€5ã€2å¼ ï¼Œå…¶ä½™è¡¥0
};

layout (binding = 1) writeonly buffer out_buffer
{
    uint hule; // è¿”å›å€¼ï¼š0=æ²¡å’Œ, 1=å’Œäº†
};

const uint NO_TILE = 0x00; // æ²¡æœ‰ç‰Œ

const uint TILE_1M = 0x11; // ä¸€è¬ ğŸ€‡
const uint TILE_2M = 0x12; // äºŒè¬ ğŸ€ˆ
const uint TILE_3M = 0x13; // ä¸‰è¬ ğŸ€‰
const uint TILE_4M = 0x14; // å››è¬ ğŸ€Š
const uint TILE_5M = 0x15; // äº”è¬ ğŸ€‹
const uint TILE_6M = 0x16; // å…­è¬ ğŸ€Œ
const uint TILE_7M = 0x17; // ä¸ƒè¬ ğŸ€
const uint TILE_8M = 0x18; // å…«è¬ ğŸ€
const uint TILE_9M = 0x19; // ä¹è¬ ğŸ€

const uint TILE_1P = 0x21; // ä¸€ç­’ ğŸ€™
const uint TILE_2P = 0x22; // äºŒç­’ ğŸ€š
const uint TILE_3P = 0x23; // ä¸‰ç­’ ğŸ€›
const uint TILE_4P = 0x24; // å››ç­’ ğŸ€œ
const uint TILE_5P = 0x25; // äº”ç­’ ğŸ€
const uint TILE_6P = 0x26; // å…­ç­’ ğŸ€
const uint TILE_7P = 0x27; // ä¸ƒç­’ ğŸ€Ÿ
const uint TILE_8P = 0x28; // å…«ç­’ ğŸ€ 
const uint TILE_9P = 0x29; // ä¹ç­’ ğŸ€¡

const uint TILE_1S = 0x31; // ä¸€ç´¢ ğŸ€
const uint TILE_2S = 0x32; // äºŒç´¢ ğŸ€‘
const uint TILE_3S = 0x33; // ä¸‰ç´¢ ğŸ€’
const uint TILE_4S = 0x34; // å››ç´¢ ğŸ€“
const uint TILE_5S = 0x35; // äº”ç´¢ ğŸ€”
const uint TILE_6S = 0x36; // å…­ç´¢ ğŸ€•
const uint TILE_7S = 0x37; // ä¸ƒç´¢ ğŸ€–
const uint TILE_8S = 0x38; // å…«ç´¢ ğŸ€—
const uint TILE_9S = 0x39; // ä¹ç´¢ ğŸ€˜

const uint TILE_1Z = 0x41; // æ± ğŸ€€
const uint TILE_2Z = 0x49; // å— ğŸ€
const uint TILE_3Z = 0x51; // è¥¿ ğŸ€‚
const uint TILE_4Z = 0x59; // åŒ— ğŸ€ƒ
const uint TILE_5Z = 0x61; // ç™½ ğŸ€†
const uint TILE_6Z = 0x69; // ç™¼ ğŸ€…
const uint TILE_7Z = 0x71; // ä¸­ ğŸ€„

const uint TILE_0M = 0x95; // èµ¤äº”è¬ ğŸ€‹
const uint TILE_0P = 0xA5; // èµ¤äº”ç­’ ğŸ€
const uint TILE_0S = 0xB5; // èµ¤äº”ç´¢ ğŸ€”

// ä¸€è¬ ğŸ€‡ -> 0x1000 
// ä¹è¬ ğŸ€ -> 0x0800
// ä¸€ç­’ ğŸ€™ -> 0x0400
// ä¹ç­’ ğŸ€¡ -> 0x0200
// ä¸€ç´¢ ğŸ€ -> 0x0100
// ä¹ç´¢ ğŸ€˜ -> 0x0080
// æ± ğŸ€€ -> 0x0040
// å— ğŸ€ -> 0x0020
// è¥¿ ğŸ€‚ -> 0x0010
// åŒ— ğŸ€ƒ -> 0x0008
// ç™½ ğŸ€† -> 0x0004
// ç™¼ ğŸ€… -> 0x0002
// ä¸­ ğŸ€„ -> 0x0001
uint mask_yaojiu(uint tile) // ä¹ˆä¹ç‰Œæ©ç ï¼Œä¾¿äºåˆ¤æ–­å›½å£«æ— åŒ
{
    if ((tile & 0x07) != 0x01)
    {
        return 0;
    }

    return 0x4000 >> (tile >> 3);
}

bool is_2(uint[2] tiles) // 1ä¸ªé›€å¤´
{
    if (tiles[0] == tiles[1]) // æ˜¯å¯¹å­
    {
        return true;
    }

    return false;
}

bool is_3(uint[3] tiles) // 1ä¸ªé¢å­
{
    if (tiles[2] == tiles[0]) // æ˜¯åˆ»å­
    {
        return true;
    }

    if (tiles[1] == tiles[0] + 1 &&
        tiles[2] == tiles[1] + 1) // æ˜¯é¡ºå­
    {
        return true;
    }

    return false;
}

bool is_3_2(uint[5] tiles) // 1ä¸ªé¢å­å’Œ1ä¸ªé›€å¤´
{
    if (tiles[2] == tiles[0])
    {
        uint[2] new_tiles = 
        {
            tiles[3], tiles[4],
        };
        if (is_2 (new_tiles)) // ç§»é™¤ä¸€ç»„åˆ»å­ï¼Œå‰©ä¸‹1ä¸ªé›€å¤´
        {
            return true;
        }
    }

    if (tiles[1] == tiles[0])
    {
        uint[3] new_tiles = 
        {
            tiles[2], tiles[3], tiles[4],
        };
        if (is_3 (new_tiles)) // ç§»é™¤ä¸€ä¸ªé›€å¤´ï¼Œå‰©ä¸‹1ä¸ªé¢å­
        {
            return true;
        }
    }

    uint count = 0;
    for (uint i = 1; i < 5; i++)
    {
        if (count < 2 && tiles[i] == tiles[0] + 1 + count)
        {
            count++;
        }
        else if (count > 0)
        {
            tiles[i - count] = tiles[i];
        }
    }

    if (count < 2)
    {
        return false;
    }

    uint[2] new_tiles = 
    {
        tiles[1], tiles[2],
    };
    if (is_2 (new_tiles)) // ç§»é™¤ä¸€ç»„é¡ºå­ï¼Œå‰©ä¸‹1ä¸ªé›€å¤´
    {
        return true;
    }

    return false;
}

bool is_3_3(uint[6] tiles) // 2ä¸ªé¢å­
{
    if (tiles[2] == tiles[0])
    {
        uint[3] new_tiles = 
        {
            tiles[3], tiles[4], tiles[5],
        };
        if (is_3 (new_tiles)) // ç§»é™¤ä¸€ç»„åˆ»å­ï¼Œå‰©ä¸‹1ä¸ªé¢å­
        {
            return true;
        }
    }

    uint count = 0;
    for (uint i = 1; i < 6; i++)
    {
        if (count < 2 && tiles[i] == tiles[0] + 1 + count)
        {
            count++;
        }
        else if (count > 0)
        {
            tiles[i - count] = tiles[i];
        }
    }

    if (count < 2)
    {
        return false;
    }

    uint[3] new_tiles = 
    {
        tiles[1], tiles[2], tiles[3],
    };
    if (is_3 (new_tiles)) // ç§»é™¤ä¸€ç»„é¡ºå­ï¼Œå‰©ä¸‹1ä¸ªé¢å­
    {
        return true;
    }

    return false;
}

bool is_3_3_2(uint[8] tiles) // 2ä¸ªé¢å­å’Œ1ä¸ªé›€å¤´
{
    if (tiles[2] == tiles[0])
    {
        uint[5] new_tiles = 
        {
            tiles[3], tiles[4], tiles[5],
            tiles[6], tiles[7],
        };
        if (is_3_2 (new_tiles)) // ç§»é™¤ä¸€ç»„åˆ»å­ï¼Œå‰©ä¸‹1ä¸ªé¢å­å’Œ1ä¸ªé›€å¤´
        {
            return true;
        }
    }

    if (tiles[1] == tiles[0])
    {
        uint[6] new_tiles = 
        {
            tiles[2], tiles[3], tiles[4],
            tiles[5], tiles[6], tiles[7],
        };
        if (is_3_3 (new_tiles)) // ç§»é™¤ä¸€ä¸ªé›€å¤´ï¼Œå‰©ä¸‹2ä¸ªé¢å­
        {
            return true;
        }
    }

    uint count = 0;
    for (uint i = 1; i < 8; i++)
    {
        if (count < 2 && tiles[i] == tiles[0] + 1 + count)
        {
            count++;
        }
        else if (count > 0)
        {
            tiles[i - count] = tiles[i];
        }
    }

    if (count < 2)
    {
        return false;
    }

    uint[5] new_tiles = 
    {
        tiles[1], tiles[2], tiles[3],
        tiles[4], tiles[5],
    };
    if (is_3_2 (new_tiles)) // ç§»é™¤ä¸€ç»„é¡ºå­ï¼Œå‰©ä¸‹1ä¸ªé¢å­å’Œ1ä¸ªé›€å¤´
    {
        return true;
    }

    return false;
}

bool is_3_3_3(uint[9] tiles) // 3ä¸ªé¢å­
{
    if (tiles[2] == tiles[0])
    {
        uint[6] new_tiles = 
        {
            tiles[3], tiles[4], tiles[5],
            tiles[6], tiles[7], tiles[8],
        };
        if (is_3_3 (new_tiles)) // ç§»é™¤ä¸€ç»„åˆ»å­ï¼Œå‰©ä¸‹2ä¸ªé¢å­
        {
            return true;
        }
    }

    uint count = 0;
    for (uint i = 1; i < 9; i++)
    {
        if (count < 2 && tiles[i] == tiles[0] + 1 + count)
        {
            count++;
        }
        else if (count > 0)
        {
            tiles[i - count] = tiles[i];
        }
    }

    if (count < 2)
    {
        return false;
    }

    uint[6] new_tiles = 
    {
        tiles[1], tiles[2], tiles[3],
        tiles[4], tiles[5], tiles[6],
    };
    if (is_3_3 (new_tiles)) // ç§»é™¤ä¸€ç»„é¡ºå­ï¼Œå‰©ä¸‹2ä¸ªé¢å­
    {
        return true;
    }

    return false;
}

bool is_3_3_3_2(uint[11] tiles) // 3ä¸ªé¢å­å’Œ1ä¸ªé›€å¤´
{
    if (tiles[2] == tiles[0])
    {
        uint[8] new_tiles = 
        {
            tiles[3], tiles[4], tiles[5],
            tiles[6], tiles[7], tiles[8],
            tiles[9], tiles[10],
        };
        if (is_3_3_2 (new_tiles)) // ç§»é™¤ä¸€ç»„åˆ»å­ï¼Œå‰©ä¸‹2ä¸ªé¢å­å’Œ1ä¸ªé›€å¤´
        {
            return true;
        }
    }

    if (tiles[1] == tiles[0])
    {
        uint[9] new_tiles = 
        {
            tiles[2], tiles[3], tiles[4],
            tiles[5], tiles[6], tiles[7],
            tiles[8], tiles[9], tiles[10],
        };
        if (is_3_3_3 (new_tiles)) // ç§»é™¤ä¸€ä¸ªé›€å¤´ï¼Œå‰©ä¸‹3ä¸ªé¢å­
        {
            return true;
        }
    }

    uint count = 0;
    for (uint i = 1; i < 11; i++)
    {
        if (count < 2 && tiles[i] == tiles[0] + 1 + count)
        {
            count++;
        }
        else if (count > 0)
        {
            tiles[i - count] = tiles[i];
        }
    }

    if (count < 2)
    {
        return false;
    }

    uint[8] new_tiles = 
    {
        tiles[1], tiles[2], tiles[3],
        tiles[4], tiles[5], tiles[6],
        tiles[7], tiles[8],
    };
    if (is_3_3_2 (new_tiles)) // ç§»é™¤ä¸€ç»„é¡ºå­ï¼Œå‰©ä¸‹2ä¸ªé¢å­å’Œ1ä¸ªé›€å¤´
    {
        return true;
    }

    return false;
}

bool is_3_3_3_3(uint[12] tiles) // 4ä¸ªé¢å­
{
    if (tiles[2] == tiles[0])
    {
        uint[9] new_tiles = 
        {
            tiles[3], tiles[4], tiles[5],
            tiles[6], tiles[7], tiles[8],
            tiles[9], tiles[10], tiles[11],
        };
        if (is_3_3_3 (new_tiles)) // ç§»é™¤ä¸€ç»„åˆ»å­ï¼Œå‰©ä¸‹3ä¸ªé¢å­
        {
            return true;
        }
    }

    uint count = 0;
    for (uint i = 1; i < 12; i++)
    {
        if (count < 2 && tiles[i] == tiles[0] + 1 + count)
        {
            count++;
        }
        else if (count > 0)
        {
            tiles[i - count] = tiles[i];
        }
    }

    if (count < 2)
    {
        return false;
    }

    uint[9] new_tiles = 
    {
        tiles[1], tiles[2], tiles[3],
        tiles[4], tiles[5], tiles[6],
        tiles[7], tiles[8], tiles[9],
    };
    if (is_3_3_3 (new_tiles)) // ç§»é™¤ä¸€ç»„é¡ºå­ï¼Œå‰©ä¸‹3ä¸ªé¢å­
    {
        return true;
    }

    return false;
}

bool is_3_3_3_3_2(uint[14] tiles) // 4ä¸ªé¢å­å’Œ1ä¸ªé›€å¤´
{
    if (tiles[2] == tiles[0])
    {
        uint[11] new_tiles = 
        {
            tiles[3], tiles[4], tiles[5],
            tiles[6], tiles[7], tiles[8],
            tiles[9], tiles[10], tiles[11],
            tiles[12], tiles[13],
        };
        if (is_3_3_3_2 (new_tiles)) // ç§»é™¤ä¸€ç»„åˆ»å­ï¼Œå‰©ä¸‹3ä¸ªé¢å­å’Œ1ä¸ªé›€å¤´
        {
            return true;
        }
    }

    if (tiles[1] == tiles[0])
    {
        uint[12] new_tiles = 
        {
            tiles[2], tiles[3], tiles[4],
            tiles[5], tiles[6], tiles[7],
            tiles[8], tiles[9], tiles[10],
            tiles[11], tiles[12], tiles[13],
        };
        if (is_3_3_3_3 (new_tiles)) // ç§»é™¤ä¸€ä¸ªé›€å¤´ï¼Œå‰©ä¸‹4ä¸ªé¢å­
        {
            return true;
        }
    }

    uint count = 0;
    for (uint i = 1; i < 14; i++)
    {
        if (count < 2 && tiles[i] == tiles[0] + 1 + count)
        {
            count++;
        }
        else if (count > 0)
        {
            tiles[i - count] = tiles[i];
        }
    }

    if (count < 2)
    {
        return false;
    }

    uint[11] new_tiles = 
    {
        tiles[1], tiles[2], tiles[3],
        tiles[4], tiles[5], tiles[6],
        tiles[7], tiles[8], tiles[9],
        tiles[10], tiles[11],
    };
    if (is_3_3_3_2 (new_tiles)) // ç§»é™¤ä¸€ç»„é¡ºå­ï¼Œå‰©ä¸‹3ä¸ªé¢å­å’Œ1ä¸ªé›€å¤´
    {
        return true;
    }

    return false;
}

bool is_qidui(uint[14] tiles) // ä¸ƒå¯¹å­
{
    uint pre_tile = NO_TILE;
    for (uint i = 0; i < 14; i += 2)
    {
        if (tiles[i] != tiles[i + 1]) // æ¯ä¸¤å¼ ä¸€ç»„ï¼Œè¿™ä¸€ç»„ä¸æ˜¯å¯¹å­
        {
            return false;
        }

        if (tiles[i] == pre_tile) // è¿™ç»„å¯¹å­å’Œä¸Šä¸€ç»„æ˜¯é‡å¤çš„å¯¹å­
        {
            return false;
        }

        pre_tile = tiles[i];
    }

    return true;
}

bool is_guoshi(uint[14] tiles) // å›½å£«æ— åŒ
{
    uint full_mask = 0;
    for (uint i = 0; i < 14; i++)
    {
        uint mask = mask_yaojiu (tiles[i]);
        if (mask == 0)
        {
            return false;
        }

        full_mask |= mask;
    }

    if (full_mask != 0x1FFF)
    {
        return false;
    }

    return true;
}

uint is_hule(uint tile_num, uint[14] tiles)
{
    if (tile_num % 3 != 2)
    {
        return 0;
    }

    // æ¸…ç†èµ¤å®
    for (uint i = 0; i < tile_num; i++)
    {
        if (tiles[i] == TILE_0M)
        {
            tiles[i] = TILE_5M;
        }
        else if (tiles[i] == TILE_0P)
        {
            tiles[i] = TILE_5P;
        }
        else if (tiles[i] == TILE_0S)
        {
            tiles[i] = TILE_5S;
        }
    }

    // å†’æ³¡æ’åº
    for (uint i = 0; i < tile_num - 1; i++)
    {
        bool dirty = false;
        for (uint j = tile_num - 1; j > i; j--)
        {
            if (tiles[j] < tiles[j - 1])
            {
                uint tmp = tiles[j];
                tiles[j] = tiles[j - 1];
                tiles[j - 1] = tmp;
                dirty = true;
            }
        }

        if (!dirty)
        {
            break;
        }
    }

    // åˆ¤æ–­ä¸€èˆ¬å½¢
    if (tile_num == 14)
    {
        if (is_3_3_3_3_2 (tiles))
        {
            return 1;
        }
    }

    if (tile_num == 11)
    {
        uint[11] new_tiles =
        {
            tiles[0], tiles[1], tiles[2],
            tiles[3], tiles[4], tiles[5],
            tiles[6], tiles[7], tiles[8],
            tiles[9], tiles[10],
        };
        if (is_3_3_3_2 (new_tiles))
        {
            return 1;
        }
    }

    if (tile_num == 8)
    {
        uint[8] new_tiles =
        {
            tiles[0], tiles[1], tiles[2],
            tiles[3], tiles[4], tiles[5],
            tiles[6], tiles[7],
        };
        if (is_3_3_2 (new_tiles))
        {
            return 1;
        }
    }

    if (tile_num == 5)
    {
        uint[5] new_tiles =
        {
            tiles[0], tiles[1], tiles[2],
            tiles[3], tiles[4],
        };
        if (is_3_2 (new_tiles))
        {
            return 1;
        }
    }

    if (tile_num == 2)
    {
        uint[2] new_tiles =
        {
            tiles[0], tiles[1],
        };
        if (is_2 (new_tiles))
        {
            return 1;
        }
    }

    // åˆ¤æ–­ä¸ƒå¯¹å­å’Œå›½å£«æ— åŒ
    if (tile_num != 14)
    {
        return 0;
    }

    if (is_qidui (tiles))
    {
        return 1;
    }

    if (is_guoshi (tiles))
    {
        return 1;
    }

    return 0;
}

void main()
{
    uint tile_num = 0;
    while (tile_num < 14 && shoupai[tile_num] != NO_TILE)
    {
        tile_num++;
    }

    hule = is_hule (tile_num, shoupai);

    return;
}

// end of file
